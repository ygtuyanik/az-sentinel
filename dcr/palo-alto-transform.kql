// Palo Alto PAN-OS CEF Transform
// Bu KQL, Palo Alto Networks PAN-OS cihazlarından gelen CEF loglarını filtreler ve dönüştürür.
// DCR transformKql alanına tek satır olarak uygulanması gerekir.
//
// Filtreler (drop edilen olaylar):
//   - DNS trafiği (DestinationPort == 53)
//   - Drop/Deny action'lı TRAFFIC olayları
//
// Dönüşümler:
//   - AdditionalExtensions: PanOSPacketsReceived, PanOSPacketsSent, start, PanOSRuleUUID alanları çıkarılır
//   - Yalnızca CommonSecurityLog şemasındaki sütunlar tutulur

source
| where ProcessName == "CEF"
| where not (
    (DeviceVendor == "Palo Alto Networks" and DeviceProduct == "PAN-OS" and Activity == 'TRAFFIC' and DestinationPort == 53)
    or (DeviceVendor == "Palo Alto Networks" and DeviceProduct == "PAN-OS" and Activity == 'TRAFFIC' and (DeviceAction == 'drop' or DeviceEventClassID == 'drop'))
    or (DeviceVendor == "Palo Alto Networks" and DeviceProduct == "PAN-OS" and Activity == 'TRAFFIC' and (DeviceAction == 'deny' or DeviceEventClassID == 'deny'))
  )
| extend AdditionalExtensions = iff(
    (DeviceVendor == "Palo Alto Networks" and DeviceProduct == "PAN-OS"),
    strcat(
      "PanOSPacketsReceived=", extract(@"PanOSPacketsReceived=([^;]+)", 1, AdditionalExtensions), ";",
      "PanOSPacketsSent=",     extract(@"PanOSPacketsSent=([^;]+)",     1, AdditionalExtensions), ";",
      "start=",                extract(@"start=([^;]+)",                1, AdditionalExtensions), ";",
      "PanOSRuleUUID=",        extract(@"PanOSRuleUUID=([^;]+)",        1, AdditionalExtensions)
    ),
    AdditionalExtensions
  )
| project
    TimeGenerated, TenantId, SourceSystem, Activity, AdditionalExtensions,
    ApplicationProtocol, CollectorHostName, CommunicationDirection, Computer,
    DestinationDnsDomain, DestinationHostName, DestinationIP, DestinationPort,
    DestinationUserID, DestinationUserName, DeviceAction, DeviceAddress,
    DeviceCustomString1, DeviceCustomString2, DeviceCustomString4, DeviceCustomString6,
    DeviceEventClassID, DeviceName, DeviceProduct, DeviceVendor,
    EndTime, EventCount, FileHash, FileName, FilePath, FlexString1,
    IndicatorThreatType, LogSeverity, MaliciousIPCountry, Message, ProcessName,
    Protocol, Reason, ReceiptTime, RemoteIP, RemotePort,
    RequestClientApplication, RequestMethod, RequestURL,
    SimplifiedDeviceAction, SourceHostName, SourceIP, SourcePort,
    SourceUserID, SourceUserName, StartTime,
    MaliciousIP, ThreatSeverity, MaliciousIPLatitude, MaliciousIPLongitude,
    ThreatDescription, ThreatConfidence,
    DeviceCustomDate1, DeviceCustomDate1Label, DeviceCustomDate2, DeviceCustomDate2Label,
    DeviceVersion, DeviceExternalID, DeviceInboundInterface, DeviceOutboundInterface,
    DeviceEventCategory, ReportReferenceLink
